CONF_VERSION = "1"
DL_DIR = "${TOPDIR}/downloads"

SDKMACHINE = "x86_64"
DISTRO = "pelux"

# Set this to match your setup
# MACHINE = "jetson-tx2"
# MACHINE = "smarcimx8m2g"
# MACHINE = "intel-corei7-64"
# MACHINE = "raspberrypi3"
# MACHINE = "qemux86-64"

# Target Static IP address, Override this to configure a static
# ip address for development purposes such as poky ssh and ping test.
STATIC_IP_ADDRESS = ""

BB_NUMBER_THREADS ?= "${@oe.utils.cpu_count()}"
PARALLEL_MAKE ?= "-j ${@oe.utils.cpu_count()}"

PACKAGE_CLASSES ?= "package_rpm"

BB_DANGLINGAPPENDS_WARNONLY = "1"

# Fix for rpm metadata clash between nativesdk-cmake and nativesdk-qtbase-tools
DIRFILES_pn-nativesdk-cmake = "1"

# Don't install any extra devel packages just because we're running on QEMU
POKYQEMUDEPS_remove = "packagegroup-core-device-devel"

# Don't build .hddimg and .iso image formats as we don't need it.
NOHDD = "1"
NOISO = "1"

IMAGE_ROOTFS_SIZE ?= "1000000"

# This is needed for SWupdate artifacts
IMAGE_FSTYPES_append = " ext3.gz"

# Removing tar.bz2 image format as it is not used.
# If needed it can be supplied through own local.conf
IMAGE_FSTYPES_remove = " tar.bz2"
IMAGE_FSTYPES_append = " wic wic.bmap wic.bz2"

SPLASH = "psplash-pelux"

LICENSE_FLAGS_WHITELIST = "commercial_faad2"

# Needs to be set to allow for machine specific overrides
WKS_FILE = ""

#
# INTEL
#

EFI_PROVIDER_intel-corei7-64 = "grub-efi"
PREFERRED_PROVIDER_iasl-native_intel-corei7-64 = "iasl-native"
WKS_FILE_intel-corei7-64 = "mkefidisk-multiboot.wks"
WIC_CREATE_EXTRA_ARGS_intel-corei7-64 = "--no-fstab-update"

# Include arp-driver, only required when building for ARP
IMAGE_INSTALL_append_intel-corei7-64 = " arp-driver"

#
# JETSON TX2
#

IMAGE_CLASSES_jetson-tx2_append = " image_types_tegra"
IMAGE_FSTYPES_jetson-tx2_append = " tegraflash"
BBMASK_jetson-tx2 .= "./meta-tegra/recipes-graphics/vulkan/vulkan_1.1.73.0.bbappend"


#
# IMX8
#

# In order to accept Freescale EULA, uncomment the following line:
#ACCEPT_FSL_EULA = "1"

BBMASK_smarcimx8m2g = "meta-fsl-bsp-release/imx/meta-bsp/recipes-connectivity/connman/"
BBMASK_smarcimx8m2g += "/meta-smarcimx8m/recipes-core/psplash/"


#
# Raspbery Pi 3
#

WKS_FILE_raspberrypi3 = "raspberrypi.wks"
RPI_USE_U_BOOT_raspberrypi3 = "1"
PREFERRED_PROVIDER_u-boot-fw-utils_raspberrypi3 = "u-boot-fw-utils"
